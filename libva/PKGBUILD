# Maintainer: cevdetta
# Contributor: cevdetta

_pkgname=libva
pkgname=${_pkgname}-waypure
pkgver=2.23.0
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('x86_64')
url="https://github.com/intel/libva"
license=('MIT')
depends=('glibc' 'libdrm' 'wayland')
makedepends=('meson' 'wayland-protocols')
optdepends=(
  'intel-media-driver: backend for Intel GPUs (>= Broadwell)'
  'libva-intel-driver: backend for Intel GPUs (<= Haswell)'
  'libva-mesa-driver: backend for AMD and NVIDIA GPUs'
)
provides=("${_pkgname}" 'libva.so' 'libva-drm.so' 'libva-wayland.so')
conflicts=("${_pkgname}")
replaces=("${_pkgname}")
source=("$url/releases/download/$pkgver/${pkgname%%-*}-$pkgver.tar.bz2")
sha256sums=('9ac190a87017bfd49743248f5df7cf3b18a99a9962175caf6bbe3f1ea41b6dbb')

build() {
  local _meson_options=(
    # -Ddriverdir=
    -Ddisable_drm=false
    -Dwith_x11=no
    -Dwith_glx=no
    -Dwith_wayland=yes
    -Dwith_win32=no
    -Denable_docs=false
  )

  arch-meson "${_pkgname}-${pkgver}" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm644 "${_pkgname}-$pkgver/COPYING" -t "$pkgdir/usr/share/licenses/${_pkgname}"
}
